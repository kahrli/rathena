//===== rAthena Script =======================================
//= War of Emperium SE - Auto-Start
//===== By: ==================================================
//= L0ne_W0lf
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: =====================================
//= rAthena SVN; RO Episode 11.3
//===== Description: =========================================
//= Auto-start for War of Emperium SE.
//= For instructions, see doc/woe_time_explanation.txt.
//===== Additional Comments: =================================
//= 1.0 Copy/Paste of the original setter.
//============================================================

-	script	Agit2_Event	-1,{
	end;

OnClock1800:	//start time for Tues(2), Thurs(4)
OnClock2000:	//end time for Tues(2), Thurs(4)
OnClock2100:	//start time for Sat(6)
OnClock2300:	//end time for Sat(6)

OnAgitInit2:
	setarray .@start[0],12,8;
	setarray .@day[0],4,0;
	setarray .region$[0],"aru","sch";
	
	for (set .@i,0; .@i<getarraysize(.region$); set .@i,.@i+1) {
		if ( gettime(4)==.@day[.@i] && gettime(3)>=.@start[.@i] && gettime(3)<.@start[.@i]+1 && !agitcheck2() ) {
			setd "$"+.region$[.@i],rand(1,5);
			set .@castle$,.region$[.@i]+"g_cas0"+getd("$"+.region$[.@i]);
			AgitStart2;
			callsub S_DisplayOwners,.@castle$,0;
			callsub S_Portals,0,.region$[.@i];
		}
		
		if ( gettime(4)==.@day[.@i] && gettime(3)==.@start[.@i]+1 && agitcheck2() ) {
			AgitEnd2;
			set .@castle$,.region$[.@i]+"g_cas0"+getd("$"+.region$[.@i]);
			callsub S_DisplayOwners,.@castle$,1;
			callsub S_Portals,1,.region$[.@i];
		}
	}
	end;
	
S_Portals:
	for(set .@i,0; .@i<getarraysize(.region$); set .@i, .@i+1) {
		for(set .@j,0; .@j<5; set .@j,.@j+1) {
			if(getarg(0)) enablenpc .region$[.@i]+"g"+.@j+"02";
			else disablenpc .region$[.@i]+"g"+.@j+"02";
		}
	}
	
	if(!getarg(0)) {
		for(set .@i,0; .@i<5; set .@i,.@i+1) {
			set .@temp,getd("$"+getarg(1));
			if(.@i==.@temp)
				enablenpc getarg(1)+"g"+.@temp+"02";
		}
	}
	return;

S_DisplayOwners:
	if (GetCastleData(getarg(0),1)) 
		Announce "The [" + GetCastleName(getarg(0)) + "] castle has been conquered by the [" + GetGuildName(GetCastleData(getarg(0),1)) + "] guild.",bc_all;
	else 
		Announce "The [" + GetCastleName(getarg(0)) + "] castle is currently unoccupied.",bc_all;
	if(getarg(1))
		Announce "The War of Emperium has come to an end.",bc_all;
	else
		Announce "The War of Emperium has begun.",bc_all;
	return;
}
